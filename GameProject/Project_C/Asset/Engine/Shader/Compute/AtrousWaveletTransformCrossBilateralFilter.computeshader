

Texture2D<float> _Value : register(t0);
Texture2D<uint> _NormalDepth : register(t1);
Texture2D<float> _Variance : register(t2); 
Texture2D<float> _HitDistance : register(t3);
Texture2D<float2> _PartialDistanceDerivatives : register(t4);
Texture2D<uint> _Tspp : register(t5);

RWTexture2D<float> _OutFilteredValue : register(u0);


cbuffer CB : register(b0)
{
    uint2 _TextureDim;
    float _DepthWeightCutoff;
    float _KernelRadiusLerfCoef;


    uint _MinKernelWidth;
    uint _MaxKernelWidth;
    float _RayHitDistanceToKernelWidthScale;
    float _RayHitDistanceToKernelSizeScaleExponent;

    
    float _MinVarianceToDenoise;
    float _ValueSigma;
    float _DepthSigma;
    float _NormalSigma;

    uint _DepthNumMantissaBits;
};
static const unsigned int _Radius = 1;
static const unsigned int _Width = 1 + 2 * _Radius;
static const float _Kernel1D[_Width] = { 0.27901, 0.44198, 0.27901 };
static const float _Kernel[_Width][_Width] =
{
    { _Kernel1D[0] * _Kernel1D[0], _Kernel1D[0] * _Kernel1D[1], _Kernel1D[0] * _Kernel1D[2] },
    { _Kernel1D[1] * _Kernel1D[0], _Kernel1D[1] * _Kernel1D[1], _Kernel1D[1] * _Kernel1D[2] },
    { _Kernel1D[2] * _Kernel1D[0], _Kernel1D[2] * _Kernel1D[1], _Kernel1D[2] * _Kernel1D[2] },
};
float DepthThreshold(float depth, float2 ddxy, float2 pixelOffset)
{
    float depthThreshold;

    float2 newDdxy = RemapDdxy(depth, ddxy, pixelOffset);
    depthThreshold = dot(1, abs(newDdxy));

    return depthThreshold;
}

void AddFilterContribution(
    inout float weightedValueSum, 
    inout float weightSum, 
    in float value, 
    in float stdDeviation,
    in float depth, 
    in float3 normal, 
    in float2 ddxy,
    in uint row, 
    in uint col,
    in uint2 kernelStep,
    in uint2 DTid)
{
    const float valueSigma = _ValueSigma;
    const float normalSigma = _NormalSigma;
    const float depthSigma = _DepthSigma;
 
    int2 pixelOffset;
    float kernelWidth;
    float varianceScale = 1;

    pixelOffset = int2(row - _Radius, col - _Radius) * kernelStep;
    int2 id = int2(DTid) + pixelOffset;

    if (IsWithinBounds(id, _TextureDim))
    {
        float iDepth;
        float3 iNormal;
        DecodeNormalDepth(_NormalDepth[id], iNormal, iDepth);
        float iValue = _Value[id];

        bool iIsValidValue = iValue != -1;
        if (!iIsValidValue || iDepth == 0)
        {
            return;
        }

        // Calculate a weight for the neighbor's contribtuion.
        // Ref:[SVGF]
        float w;
        {
            // Value based weight.
            // Lower value tolerance for the neighbors further apart. Prevents overbluring sharp value transitions.
            // Ref: [Dammertz2010]
            const float errorOffset = 0.005f;
            float valueSigmaDistCoef = 1.0 / length(pixelOffset);
            float e_x = -abs(value - iValue) / (valueSigmaDistCoef * valueSigma * stdDeviation + errorOffset);
            float w_x = exp(e_x);

            // Normal based weight.
            float w_n = pow(max(0, dot(normal, iNormal)), normalSigma);

            // Depth based weight.
            float w_d;
            {
                float2 pixelOffsetForDepth = pixelOffset;
                float depthFloatPrecision = FloatPrecision(max(depth, iDepth), _DepthNumMantissaBits);
                float depthThreshold = DepthThreshold(depth, ddxy, pixelOffsetForDepth);
                float depthTolerance = depthSigma * depthThreshold + depthFloatPrecision;
                float delta = abs(depth - iDepth);
                delta = max(0, delta - depthFloatPrecision); // Avoid distinguising initial values up to the float precision. Gets rid of banding due to low depth precision format.
                w_d = exp(-delta / depthTolerance);

                // Scale down contributions for samples beyond tolerance, but completely disable contribution for samples too far away.
                w_d *= w_d >= _DepthWeightCutoff;
            }

            // Filter kernel weight.
            float w_h = _Kernel[row][col];

            // Final weight.
            w = w_h * w_n * w_x * w_d;
        }

        weightedValueSum += w * iValue;
        weightSum += w;
    }
}

[numthreads(16,16, 1)]
void cs_main(uint2 DTid : SV_DispatchThreadID, uint2 Gid : SV_GroupID)
{
    if (!IsWithinBounds(DTid, _TextureDim))
    {
        return;
    }

    // Initialize values to the current pixel / center filter kernel value.
    float value = _Value[DTid];
    float3 normal;
    float depth;
    DecodeNormalDepth(_NormalDepth[DTid], normal, depth);
    
    bool isValidValue = value != -1;
    float filteredValue = value;
    float variance = _Variance[DTid];

    if (depth != 0)
    {
        float2 ddxy = _PartialDistanceDerivatives[DTid];
        float weightSum = 0;
        float weightedValueSum = 0;
        float stdDeviation = 1;

        if (isValidValue)
        {
            float w = _Kernel[_Radius][_Radius];
            weightSum = w;
            weightedValueSum = weightSum * value;
            stdDeviation = sqrt(variance);
        }

        // Adaptive kernel size
        // Scale the kernel span based on AO ray hit distance. 
        // This helps filter out lower frequency noise, a.k.a. boiling artifacts.
        // Ref: [RTGCH19]
        uint2 kernelStep = 0;
        if (isValidValue)
        {
            float avgRayHitDistance = _HitDistance[DTid];

            float perPixelViewAngle = (45.f / _TextureDim.y) * JG_PI / 180.0; 
            float tan_a = tan(perPixelViewAngle);
            float2 projectedSurfaceDim = ApproximateProjectedSurfaceDimensionsPerPixel(depth, ddxy, tan_a);

            // Calculate a kernel width as a ratio of hitDistance / projected surface dim per pixel.
            // Apply a non-linear factor based on relative rayHitDistance. This is because
            // average ray hit distance grows large fast if the closeby occluders cover only part of the hemisphere.
            // Having a smaller kernel for such cases helps preserve occlusion detail.
            float t = min(avgRayHitDistance / 22.0, 1); // 22 was selected empirically
            float k = _RayHitDistanceToKernelWidthScale * pow(t, _RayHitDistanceToKernelSizeScaleExponent);
            kernelStep = max(1, round(k * avgRayHitDistance / projectedSurfaceDim));

            uint2 targetKernelStep = clamp(kernelStep, (_MinKernelWidth - 1) / 2, (_MaxKernelWidth - 1) / 2);

            // TODO: additional options to explore
            // - non-uniform X, Y kernel radius cause visible streaking. Use same step across both X, Y? That may overblur one dimension at sharp angles.
            // - use larger kernel on lower tspp. 
            // - use varying number of cycles for better spatial coverage over time, depending on the target kernel step. More cycles on larger kernels.
            uint2 adjustedKernelStep = lerp(1, targetKernelStep, _KernelRadiusLerfCoef); 
            kernelStep = adjustedKernelStep;
        }

        if (variance >= _MinVarianceToDenoise)
        {
            // Add contributions from the neighborhood.
            [unroll]
            for (uint r = 0; r < _Width; r++)
            [unroll]
            for (uint c = 0; c < _Width; c++)
                if (r != _Radius || c != _Radius)
                    AddFilterContribution(
                        weightedValueSum, 
                        weightSum, 
                        value, 
                        stdDeviation, 
                        depth, 
                        normal, 
                        ddxy, 
                        r, 
                        c, 
                        kernelStep, 
                        DTid);
        }

        float smallValue = 1e-6f;
        if (weightSum > smallValue)
        {
            filteredValue = weightedValueSum / weightSum;
        }
        else
        {
            filteredValue = -1;
        }
    }

    _OutFilteredValue[DTid] = filteredValue;
}